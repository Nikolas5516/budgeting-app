import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.5'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'com.diffplug.spotless' version '6.22.0'
    id 'org.springdoc.openapi-gradle-plugin' version '1.9.0'
    id 'org.openapi.generator' version '7.16.0'
}

group = 'cloudflight.integra'
version = '0.0.1-SNAPSHOT'
description = 'backend'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(24)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // Spring Boot Web
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    // Spring Security
    implementation "org.springframework.boot:spring-boot-starter-security"

    // Jwt
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'

    // Validation
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.hibernate.validator:hibernate-validator:8.0.1.Final'
    implementation 'jakarta.validation:jakarta.validation-api:3.1.0'
    implementation 'com.google.code.findbugs:jsr305:3.0.2'

    // Springdoc OpenAPI (UI + starter)
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.13'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'

    // DB dependency
    implementation 'org.postgresql:postgresql:42.7.7'
    testImplementation 'com.h2database:h2'
    runtimeOnly 'com.h2database:h2'

    // Test dependencies
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.0'

    // archunit
    testImplementation 'com.tngtech.archunit:archunit:1.4.1'  //AnalyzeClasses
    testImplementation 'com.tngtech.archunit:archunit-junit5:1.4.1'

    // Jackson
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.1'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml'

    // Logging
    implementation 'org.slf4j:slf4j-api'

    // Code generation
    implementation 'org.mapstruct:mapstruct:1.6.2'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.1'

    // Flyway for DB migrations
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.flywaydb:flyway-database-postgresql'



}

tasks.named('test') {
    useJUnitPlatform()
}

spotless {
    java {
        palantirJavaFormat()
    }
}

openApi {
    apiDocsUrl.set("http://localhost:8080/v3/api-docs")
    outputDir.set(file("$buildDir/generated"))
    outputFileName.set("money-mind.yaml")
    waitTimeInSeconds.set(20)

    customBootRun {
        args.set(["--spring.profiles.active=openapi"])
    }
}

tasks.register('generateAngularClient', GenerateTask) {
    dependsOn tasks.generateOpenApiDocs
    generatorName.set("typescript-angular")
    inputSpec.set(file("$buildDir/generated/money-mind.yaml").toURI().toString())

    outputDir.set("$projectDir/../frontend/src/app/api")
    apiPackage.set("api")
    modelPackage.set("models")
    additionalProperties.set([
            ngVersion: "17.0.0",
            npmName  : "@myorg/api-client"
    ])
}

tasks.build {
    dependsOn tasks.generateAngularClient
}